group = PROJ_GROUP
version = PROJ_VERSION

ext {
    UsingSonatype = hasProperty('UsingSonatype')
    isReleaseVersion = !version.endsWith("-SNAPSHOT")
// Defind in ~/.gradle/gradle.properties
    ReleaseUrl = UsingSonatype ? SONATYPE_RELEASE_URL : LOCAL_RELEASE_URL
    SnapshotUrl = UsingSonatype ? SONATYPE_SNAPSHOT_URL : LOCAL_SNAPSHOT_URL
    Username = UsingSonatype ? SONATYPE_USER : LOCAL_USERNAME
    Password = UsingSonatype ? SONATYPE_PASS : LOCAL_PASSWORD
    publishAar = hasProperty('aar') ? true : false
}

apply plugin: 'maven'

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task myJavadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += configurations.compile
}
task javadocJar(type: Jar, dependsOn: myJavadoc) {
    classifier = 'javadoc'
    from myJavadoc.destinationDir
}

myJavadoc {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "http://docs.oracle.com/javase/7/docs/api"
        title "Vehicle Keyboard $PROJ_VERSION"
    }
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: isReleaseVersion ? ReleaseUrl : SnapshotUrl)
            repository(url: ReleaseUrl) {
                authentication(userName: Username, password: Password)
            }
            snapshotRepository(url: SnapshotUrl) {
                authentication(userName: Username, password: Password)
            }
            pom.groupId = PROJ_GROUP
            pom.artifactId = PROJ_ARTIFACT_ID
            pom.version = version
            pom.project {
                inceptionYear PROJ_INCEPTION_YEAR
                name PROJ_NAME
                packaging publishAar ? 'aar' : 'jar'
                description PROJ_DESCRIPTION
                url PROJ_URL
                scm {
                    url SCM_URL
                    connection SCM_CONNECTION
                    developerConnection SCM_DEV_CONNECTION
                }
                licenses {
                    license {
                        name LICENSE_NAME
                        url LICENSE_URL
                        distribution LICENSE_DISTRIBUTION
                    }
                }
                developers {
                    developer {
                        id "yoojia"
                        name "ChenYongJia"
                        email "chenyongjia@parkingwang.com"
                    }
                }
            }
        }
    }
}